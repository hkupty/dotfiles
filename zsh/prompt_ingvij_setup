#
# A simple theme inspired by the Sorin and PeepCode themes.
#
# Authors:
#   Daniel Møller Kristensen <damoekri@icloud.com>
#
# Screenshots:
#   http://i.imgur.com/AX9HnPF.png
#

# Load dependencies.
pmodload 'helper'

function prompt_damoekri_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_damoekri_pwd="$MATCH"
    unset MATCH
  else
    _prompt_damoekri_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_damoekri_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_damoekri_pwd

  vcs_info
}

function prompt_damoekri_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_damoekri_precmd

  local vcs_info_color='%F{242}'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{green}»%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr '!'
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' actionformats "%S" "%r/%s/%b %u%c (%a)"
  zstyle ':vcs_info:*' formats "%S" "%r/%s/%b %u%c"
  zstyle ':vcs_info:*' nvcsformats "%~" ""

  # Define prompts.
  PROMPT='%F{cyan}${_prompt_damoekri_pwd}%f${editor_info[keymap]} '
  RPROMPT="${vcs_info_color}"'${vcs_info_msg_1_}'"%f"
}

prompt_damoekri_setup "$@"
